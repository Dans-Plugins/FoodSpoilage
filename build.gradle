plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

repositories {
    mavenLocal()
    maven { url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/') }
    maven { url = uri('https://oss.sonatype.org/content/groups/public/') }
    maven { url = uri('https://jitpack.io') }
    maven { url = uri('https://repo.maven.apache.org/maven2/') }
    maven { url = uri('https://repo.rpkit.com/repository/maven-releases/') }
}

dependencies {
    implementation 'com.github.Preponderous-Software:ponder:1.1'
    implementation 'org.spigotmc:spigot-api:1.14.1-R0.1-SNAPSHOT'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
    implementation 'com.rpkit:rpk-core-bukkit:2.3.0:all'
    implementation 'com.rpkit:rpk-food-lib-bukkit:2.3.0:all'
}

group = 'DansPlugins'
version = '3.0.1'
description = 'FoodSpoilage'
java.sourceCompatibility = JavaVersion.VERSION_16

import org.apache.tools.ant.filters.ReplaceTokens
processResources {
    filter ReplaceTokens, tokens: [
            'version': version
    ]
}

shadowJar {
    archiveClassifier.set('')
    mergeServiceFiles()
    dependencies {
        include(dependency('com.github.Preponderous-Software:ponder'))
        include(dependency('org.bstats:bstats-bukkit'))
        include(dependency('org.bstats:bstats-base'))
    }
    relocate 'preponderous.ponder', 'spoilagesystem.shadow.preponderous.ponder'
    relocate 'org.intellij', 'spoilagesystem.shadow.org.intellij'
    relocate 'org.jetbrains', 'spoilagesystem.shadow.org.jetbrains'
    relocate 'org.bstats', 'spoilagesystem.shadow.org.bstats'
}

artifacts {
    archives shadowJar
}

publishing {
    publications {
        maven(MavenPublication) { publication ->
            from(project.shadow.component(publication))
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
